buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.7.1'
    }
}
apply plugin: 'android'

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 19
    buildToolsVersion '19'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19
        versionName '1.0'
        versionCode versionCode()
    }

    signingConfigs {
        release {
            storeFile file(homeDir() + "/.spothole/keys/spothole-key.keystore")
            keyAlias "spothole"
            storePassword password()
            keyPassword password()
        }
    }

    buildTypes {

        release {
            signingConfig signingConfigs.release
        }
    }

    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

}

dependencies {
    compile 'com.android.support:support-v4:13.0.0'
    compile 'com.google.android.gms:play-services:4.0.30'
    compile 'com.google.guava:guava:14.0.1'
    compile project(':pothole-client')

    instrumentTestCompile 'org.hamcrest:hamcrest-all:1.3'
}

def password() {
    return file(homeDir() + "/.spothole/keys/password").text
}

def homeDir() {
    return System.properties.getProperty("user.home")
}

def versionCode() {
    // use Jenkins build number if available
    return Integer.parseInt(System.getenv('BUILD_NUMBER') ?: '0')
}
